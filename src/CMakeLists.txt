# SPDX-License-Identifier: BSD-3-Clause
# SPDX-FileCopyrightText: 2020-2021 Harald Sitter <sitter@kde.org>

add_definitions(-DTRANSLATION_DOMAIN=\"plasma_disks\")

set(kded_SRCS
    smartdata.cpp smartdata.h
    smartmonitor.cpp smartmonitor.h
    smartctl.cpp smartctl.h
    smartnotifier.cpp smartnotifier.h
    dbusobjectmanagerserver.cpp
    device.cpp
    devicenotifier.cpp
    soliddevicenotifier.cpp soliddevicenotifier.h
    instabilities.cpp instabilities.h
    smartfailure.cpp smartfailure.h
)

if(WITH_SIMULATION)
    list(APPEND kded_SRCS
        simulationdevicenotifier.cpp simulationdevicenotifier.h
        simulationctl.cpp simulationctl.h
        ../autotests/fixtures/simulation.qrc)
    add_definitions(-DWITH_SIMULATION)
endif()

ecm_qt_declare_logging_category(
    kded_SRCS
    HEADER "kded_debug.h"
    IDENTIFIER "KDED"
    CATEGORY_NAME "org.kde.plasma.disks"
    DESCRIPTION "Plasma Disks"
    EXPORT PlasmaDisks
)

add_library(statickdedsmart STATIC ${kded_SRCS})
set_property(TARGET statickdedsmart PROPERTY POSITION_INDEPENDENT_CODE ON)
target_include_directories(statickdedsmart
    PUBLIC
        "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR};${CMAKE_CURRENT_BINARY_DIR}>")
target_link_libraries(statickdedsmart
    PUBLIC
    Qt${QT_MAJOR_VERSION}::Core
    Qt${QT_MAJOR_VERSION}::DBus
    KF5::Solid
    KF5::I18n
    KF5::Notifications
    KF5::CoreAddons
    KF5::DBusAddons
    KF5::Service
    KF5::KIOGui
    KF5::AuthCore
)

add_library(kded-smart MODULE main.cpp)
set_target_properties(kded-smart PROPERTIES OUTPUT_NAME smart)
target_link_libraries(kded-smart statickdedsmart)
install(TARGETS kded-smart DESTINATION ${KDE_INSTALL_PLUGINDIR}/kf${QT_MAJOR_VERSION}/kded)

kauth_install_actions(org.kde.kded.smart org.kde.kded.smart.actions)
add_executable(kded-smart-helper helper.cpp)
target_link_libraries(kded-smart-helper KF5::AuthCore KF5::ConfigCore KF5::I18n)

kauth_install_helper_files(kded-smart-helper org.kde.kded.smart root)
install(TARGETS kded-smart-helper DESTINATION ${KAUTH_HELPER_INSTALL_DIR})

install(FILES org.kde.kded.smart.notifyrc DESTINATION ${KDE_INSTALL_KNOTIFYRCDIR})

add_subdirectory(kcm)
